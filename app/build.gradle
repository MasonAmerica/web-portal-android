apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def props = project.getProperties()

def url, allowBrowsing

def getVersionCode = { ->
    return project.hasProperty('versionCode') ? versionCode.toInteger() : 1
}

def getVersionName = { ->
    return project.hasProperty('versionName') ? versionName : "1.0.0"
}

task(configureApp) {
    url = props.get("url")
    allowBrowsing = props.get("allowBrowsing")
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.trevorhalvorson.portal"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField("String", "URL", "$url")
            buildConfigField("Boolean", "ALLOW_BROWSING", "$allowBrowsing")
        }
        debug {
            buildConfigField("String", "URL", '"https://www.wikipedia.org/"')
            buildConfigField("Boolean", "ALLOW_BROWSING", "true")
        }
    }
    lintOptions {
        abortOnError true
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
